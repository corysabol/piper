/*
Simple example:

=> pipeline
  a = 1
  b = "string"
  t :> foo()
  u :> bar()
  f -> t > u
  >> 
    f > 
    a |> [
      b |> [c,d], e |> [f,g]
    ]

*/

file = { SOI ~ pipeline ~ EOI }

pipeline = { "=>" ~ identifier ~ variable* ~ (taskdecl | flowdecl | flowcall)* }

identifier = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

variable = { identifier ~ "=" ~ literal }

taskdecl = { identifier ~ ":>" ~ taskcall }
taskcall = { identifier ~ "(" ~ ")" }

flowdecl   = { identifier ~ flowassign }
flowassign = { "->" ~ flowexpr* }
flowcall   = { ">>" ~ flowexpr* }

flowexpr       = {
    (identifier | flowcollection) ~ ((">" | "|>") ~ (identifier | flowcollection))*
}
flowcollection = {
    "[" ~ (flowexpr ~ ("," ~ flowexpr)*) ~ "]"
}

literal =  { string | number | boolean | unit }
string  =  { "\"" ~ (("\\\"") | (!"\"" ~ ANY))* ~ "\"" }
number  = @{ "-"? ~ ("0" | ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)?) }
boolean =  { "true" | "false" }
unit    =  { "()" }

WHITESPACE = _{ (" " | "\t" | NEWLINE)+ }
